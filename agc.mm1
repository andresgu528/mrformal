-- Minimalistic axiomatization of classical first order logic, using Metamath Zero
-- Author: Andres Guarin
-- E-mail: anguco528@gmail.com

provable sort wff;

-- Language
-- The language will consist of the D logical connective, that represents the NAND
--  logical operator, and a countable number of variables that represent propositions.

--| The only logical connective needed, other ones are defined in terms of this.
--| Polish notation will be used.
term nand : wff > wff > wff;
prefix nand : $D$ prec max;

-- Inference rules

--| Nicod's ponens, the only inference rule needed in this propositional calculus.
axiom pnl (a b c : wff) (h1 : $D a D b c$) (h2 : $a$) : $c$;

--| Logical axioms

--| Wajsberg's axiom, from this axiom scheme and Nicod's ponens, all classical logic can
--| be derived.
--| Short: D D a D b c D -> D d c D a d D a D a b
--| Hypothesis: D a D b c
--| Conclusion: D a D a b
axiom axl (a b c d : wff) : $D D a D b c D D D d c D D a d D a d D a D a b$;


--| Define the not logical connective.
def not (a : wff) : wff = $D a a$;
prefix not : $~$ prec max;
--| Define the and logical connective.
def and (a b : wff) : wff = $D D a b D a b$;
prefix and : $/\$ prec max;
--| Define the or logical connective.
def or (a b : wff) : wff = $D D a a D b b$;
prefix or : $\/$ prec max;
--| Define the implication logical connective.
def imp (a b : wff) : wff = $D a D b b$;
prefix imp : $->$ prec max;

-- Derived induction rules.
--| Nicod ponens with and.
pub theorem pn_a (a b c : wff) (h1 : $D a /\ b c$) (h2 : $a$) : $D b c$
    = '(pnl h1 h2);
--| Modus ponens.
pub theorem mp(a b : wff) (h1 : $-> a b$) (h2 : $a$)  : $b$
    = '(pnl h1 h2);

--| $th 1$ 
--| Short: D D a D b c D D a D b c -> D d c D a d
--| Hypothesis: D a D b c
--| Conclusion: D D d c D D a d D a d
pub theorem th_p00 (a b c d : wff) : $D D a D b c D D a D b c D D d c D D a d D a d$
    = '(pnl
        (! axl $D a D b c$  $D D d c D D a d D a d$  $D a D a b$  a)
        (! axl a b c d));
--| th 2.1
--| Short: -> D a D b c D a D b c
--| Hypothesis: D a D b c
--| Conclusion: D a D b c
--| No new theorems can be derived from this in the first position of pnl
pub theorem th_p0p00 (a b c : wff) : $D D a D b c D D a D b c D a D b c$
    = '(pnl
        (! axl $D a D b c$  $D a D b c$  $D D a c D D a a D a a$  a)
        (! th_p00 a b c a));
--| th 2.2
--| Short: -> D a D b D b c D D b D c d a
--| Hypothesis: D a D b D b c
--| Conclusion: D D b D c d a
pub theorem th_pp000(a b c d : wff) : $D D a D b D b c D D D b D c d a D D b D c d a$
    = '(pnl
        (! th_p00 $D b D c d$  $D D a d /\ b a$  $D b D b c$  a)
        (! axl b c d a));
-- TODO: Add short, hypothesis, conclusion to everything below
pub theorem th_2_3 (a b c d e : wff) : $-> D a -> D b c D d b D D d D e c a$
    = '(pnl
        (! th_p00 $D d D e c$  $D d D e c$  $-> D b c D d b$  a)
        (! th_p00 d e c b));
pub theorem th_3_1 (a b c d : wff) : $D D a D b D b c D D a D b D b c D D b D c d a$
    = '(pnl
        (! axl $D a D b D b c$  $D D b D c d a$  $D D b D c d a$  a)
        (! th_pp000 a b c d));
pub theorem th_3_2 (a b c d e : wff) : $D D a -> D b c D d b D D a -> D b c D d b D D d D e c a$
    = '(pnl
        (! axl $D a -> D b c D d b$  $D D d D e c a$  $D D d D e c a$  a)
        (! th_2_3 a b c d e));
pub theorem th_3_3 (a b c d : wff) : $-> D a D b D c d D D b D c d a$
    = '(pnl
        (! th_p00 $D b D c d$  $D b D c d$  $D b D c d$  a)
        (! th_p0p00 b c d));
pub theorem th_3_4 (a b c d e : wff) : $-> D a D D b D c d e D D e D b D b c a$
    = '(pnl
        (! th_p00 $D e D b D b c$  $D D b D c d e$  $D D b D c d e$  a)
        (! th_pp000 e b c d));
pub theorem th_3_5 (a b c d e : wff) : $-> D a D D b D a c d D D d -> D e c D b e a$
    = '(pnl
        (! th_p00 $D d -> D e c D b e$  $D D b D a c d$  $D D b D a c d$  a)
        (! th_2_3 d e c b a));
-- TODO: Check missing. Done: th21 ax, th21 th1, th22 ax, th22 th1, th22 th22, th23 ax, th23 th1